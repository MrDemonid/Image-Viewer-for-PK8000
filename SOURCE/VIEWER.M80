; Модуль является адаптацией просмотровщика msx и спектрумовских картинок
; (в т.ч. мультиколорных) на ПК8000, Ивана Городецкого, 2008-2020

$title  ('Viewer Externals')
        NAME    VIEWER

        CSEG


        public ChkTyp
        public GetTyp


        public doSTD
        public doTmx
        public doMlt
        public doMc
        public doIfl
        public doSC2
        public doVM2



CHRGEN  equ 0FA09h      ; адрес знакогенератора реж. 0 и 1,
SCBUF2  equ CHRGEN      ; или буфера экрана реж. 2
SGRADR  equ 0FA0Fh      ; адрес массива изображения реж. 2
COLTBL  equ 0FA11h      ; адрес массива цвета реж. 2

PALTBL  equ 0B800h      ; свободная область за массивом цвета



MOVB macro
        mov     a, m
        stax    d
        inr     h
        inx     d
endm

UNLATR macro
        mov     c, m
        ldax    b
        stax    d
        inr     h
        inx     d
endm


MOVL macro
        mov     a, m
        stax    d
        dad     b
        inx     d
endm



;------------------------------------------------------------------------
; проверка расширения файла
;------------------------------------------------------------------------
; на входе:
;    BC - имя файла в формате CP/M с диском (' FILENAMEEXT')
; на выходе:
;    A  - 0: неизвестный формат, иначе - графический
ChkTyp:
        lxi     H, 9
        dad     B
        lxi     B, szExts+3
    @cktlp:
        ; first byte
        ldax    B
        ora     A
        jz      @ckunk
        push    H
        inx     B
        cmp     M
        jnz     @cknt1
        inx     H
        ; two byte
        ldax    B
        inx     B
        cmp     M
        jnz     @cknt2
        inx     H
        ; three byte
        ldax    B
        cmp     M
        jnz     @cknt2
        ; found
        pop     B
        ori     -1
        ret

    @cknt1:
        inx     B
    @cknt2:
        inx     B
        pop     H
        jmp     @cktlp
  @ckunk:
        xra     A
        ret


;------------------------------------------------------------------------
; определение типа граф. файла
;------------------------------------------------------------------------
; на входе:
;    BC - имя файла в формате CP/M с диском (' FILENAMEEXT')
;    DE - его размер в 128-ми байтных секторах
; на выходе:
;    A  - тип: 1 - std SCR, 2 - timex SCR, 3 - MLT, 4 - MC, 5 - IFL, 6 - SC2, 7 - VM2 (PK8000)
GetTyp:
        mov     A, D
        ora     A
        jnz     @ckunk          ; размер превышает 255*128, уходим
        lxi     H, 9
        dad     B               ; HL = &ext
        lxi     B, szExts
    @gttlp:
        ; first byte
        ldax    B
        ora     A
        jz      @ckunk          ; ничего не нашли
        push    H
        inx     B
        cmp     M
        jnz     @gttn1
        inx     H
        ; two byte
        ldax    B
        inx     B
        cmp     M
        jnz     @gttn2
        inx     H
        ; three byte
        ldax    B
        cmp     M
        jnz     @gttn2
        ; проверяем размер
        lxi     H, grSize
        mov     A, L
        add     D
        mov     L, A
        mov     A, H
        aci     0
        mov     H, A
        mov     A, M
        cmp     E
        jnz     @gttn2
        ; found
        pop     H
        mov     A, D
        inr     A
        ret

    @gttn1:
        inx     B
    @gttn2:
        inx     B
        inr     D
        pop     H
        jmp     @gttlp


szExts: db 'SCRSCRMLTMC IFLSC2VM2',0
grSize: db 54,96,96,96,72,113,97




;=============================================================================
;*****************************************************************************
;*****************************************************************************
;*********** Стандартный спектрумовский экран (.SCR    6912 bytes) ***********
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
;
doSTD:
        push    B
        call    ZxTrans         ; готовим таблицу перекодировки цветов
        ;
        lhld    SGRADR
        xchg                    ; DE = адрес массива изображения реж. 2
        pop     H
        call    ZxNLinear

        xchg
        lhld    COLTBL
        xchg
        call    atr768
        ret

;=============================================================================
;*****************************************************************************
;*****************************************************************************
;************************* Timex (.SCR   12288 bytes) ************************
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
;
doTmx:
        push    B
        call    ZxTrans         ; готовим таблицу перекодировки цветов
        ;
        lhld    SGRADR
        xchg                    ; DE = адрес массива изображения реж. 2
        pop     H
        call    ZxNLinear

        xchg
        lhld    COLTBL
        xchg
        call    atrNLinear
        ret

;=============================================================================
;*****************************************************************************
;*****************************************************************************
;********************** Multicolor (.MLT   12288 bytes) **********************
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
doMlt:
        push    B
        call    ZxTrans         ; готовим таблицу перекодировки цветов
        ;
        lhld    SGRADR
        xchg                    ; DE = адрес массива изображения реж. 2
        pop     H
        call    ZxNLinear

        xchg
        lhld    COLTBL
        xchg
        call    atrLinear
        ret

;=============================================================================
;*****************************************************************************
;*****************************************************************************
;********************** Multicolor (.MC   12288 bytes) ***********************
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
doMc:
        push    B
        call    ZxTrans         ; готовим таблицу перекодировки цветов
        ;
        lhld    SGRADR
        xchg                    ; DE = адрес массива изображения реж. 2
        pop     H
        call    ZxLinear

        xchg
        lhld    COLTBL
        xchg
        call    atrLinear
        ret

;=============================================================================
;*****************************************************************************
;*****************************************************************************
;********************** Multicolor (.IFL   9216 bytes) ***********************
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
doIfl:
        push    B
        call    ZxTrans         ; готовим таблицу перекодировки цветов
        ;
        lhld    SGRADR
        xchg                    ; DE = адрес массива изображения реж. 2
        pop     H
        call    ZxNLinear

        xchg
        lhld    COLTBL
        xchg
        call    atr3072
        ret


;=============================================================================
;*****************************************************************************
;*****************************************************************************
;************************** MSX (.SC2   12288 bytes) *************************
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
doSC2:
        lxi     H, 7
        dad     B
        push    H
        call    MsxTrans        ; готовим таблицу перекодировки цветов

        ; копируем на экран
        lhld    SGRADR
        xchg                    ; DE = адрес массива изображения реж. 2
        pop     H               ; HL = загруженный файл
        lxi     B, 6144+768     ; img buffer(32*8)*24 + screen buffer(32*24)
    @igsc2:
        mov     A, M
        stax    D
        inx     H
        inx     D
        dcx     B
        mov     A, B
        ora     C
        jnz     @igsc2


        lxi     B, 8192-(6144+768)
        dad     B               ; HL = адрес массива цвета исходника
        xchg
        lhld    COLTBL
        xchg                    ; DE = адрес массива цвета реж. 2
        lxi     B, 6144
        push    B               ; [SP+2] = counter = (32*8)*24
        mvi     B, PALTBL shr 8
    @clsc2:
        mov     C, M
        ldax    B
        stax    D
        inx     H
        inx     D
        ; уменьшаем счетчик
        xthl                    ; HL = counter
        dcx     H
        mov     A, H
        ora     L
        xthl
        jnz     @clsc2

        pop     H               ; restore stack
        ret



;=============================================================================
;*****************************************************************************
;*****************************************************************************
;************************ PK8000 (.VM2   12292 bytes) ************************
;*****************************************************************************
;*****************************************************************************
;=============================================================================
; на входе:
;     BC        - адрес загруженного файла
doVM2:
        ; проверяем сигнатуру
        ldax    B
        inx     B
        cpi     'P'
        rnz
        ldax    B
        inx     B
        cpi     'C'
        rnz
        ldax    B
        inx     B
        cpi     '8'
        rnz
        inx     B
        ; копируем массив изображения на экран
        lhld    SGRADR
        lxi     D, 6144
    @igvm2:
        ldax    B
        inx     B
        mov     M, A
        inx     H
        dcx     D
        mov     A, E
        ora     D
        jnz     @igvm2

        ; копируем массив цвета на экран
        lhld    COLTBL
        lxi     D, 6144
    @icvm2:
        ldax    B
        inx     B
        mov     M, A
        inx     H
        dcx     D
        mov     A, E
        ora     D
        jnz     @icvm2

        ret

;=============================================================================
;*****************************************************************************
;*****************************************************************************
;********************** Вспомогательные функции вывода ***********************
;*****************************************************************************
;*****************************************************************************
;=============================================================================


;------------------------------------------------------------------------
; подготовка таблицы перекодировки цветов  MSX->ПК8000
;------------------------------------------------------------------------
MsxTrans:
        lxi     D, PALTBL
    @cvsc2:
        mov     A, E
        ani     00001111b
        lxi     H, MsxCol
        add     L
        mov     L, A
        mov     A, H
        aci     0               ; HL = ClrTab + LO(TabSc2)
        mov     H, A
        mov     A, M
        rlc
        rlc
        rlc
        rlc
        mov     C, A             ;цвет фона
        mov     A, E
        rrc
        rrc
        rrc
        rrc
        ani     00001111b
        lxi     H, MsxCol
        add     L
        mov     L, A
        mov     A, H
        aci     0
        mov     H, A
        mov     A, M             ;цвет изображения
        ora     C
        stax    D
        inr     E
        jnz     @cvsc2
        ret

;------------------------------------------------------------------------
; подготовка таблицы перекодировки цветов ZX->ПК8000
;------------------------------------------------------------------------
ZxTrans:
        lxi     D, PALTBL
    @scrmt:
        mov     A, E
        ani     00000111b
        lxi     H, ZXCol
        add     L
        mov     L, A
        mov     A, H
        aci     0
        mov     H, A
        mov     C, M             ;цвет изображения
        mov     A, E
        rrc
        rrc
        rrc
        ani     00000111b
        lxi     H, ZXCol
        add     L
        mov     L, A
        mov     A, H
        aci     0
        mov     H, A
        mov     A, M             ;цвет фона
        rlc
        rlc
        rlc
        rlc
        ora     C
        mov     C, A
        mov     A, E
        ani     01000000b
        jz      @nobrg
        mvi     A, 00010001b    ; bright
    @nobrg:
        ora     C
        stax    D
        inr     E
        jnz     @scrmt
        ret








;------------------------------------------------------------------------
; вывод массива изображения (не линейного)
;------------------------------------------------------------------------
; на входе:
;    DE - адрес массива изображения реж. 2
;    HL - адрес загруженного файла
zxNLinear:
        mvi     a, 3
  @snllp:
        push    psw
        push    h
        mvi     b, 8             ;в трети 8 строк
  @snlth:
        push    h
        mvi     c, 32            ;32 символа в строке
  @snlln:
        push    h
        MOVB
        MOVB
        MOVB
        MOVB
        MOVB
        MOVB
        MOVB
        MOVB
        pop     h
        inx     h
        dcr     c
        jnz     @snlln
        pop     h
        push    d
        lxi     d, 32
        dad     d
        pop     d
        dcr     b
        jnz     @snlth
        pop     h
        push    d
        lxi     d, 2048
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @snllp
        ret

;------------------------------------------------------------------------
; вывод массива изображения (линейного)
;------------------------------------------------------------------------
zxLinear:
        mvi     a, 3
        lxi     b, 32
  @slmlp:
        push    psw
        push    h
        mvi     a, 8             ;в трети 8 строк
  @slthr:
        push    psw
        push    h
        mvi     a, 32            ;32 символа в строке
  @sllin:
        push    psw
        push    h
        MOVL
        MOVL
        MOVL
        MOVL
        MOVL
        MOVL
        MOVL
        MOVL
        pop     h
        inx     h
        pop     psw
        dcr     a
        jnz     @sllin
        pop     h
        inr     h
        pop     psw
        dcr     a
        jnz     @slthr
        pop     h
        push    d
        lxi     d, 2048
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @slmlp
        ret










;------------------------------------------------------------------------
; распаковка стандартного спектрумовского массива цвета 8x8
;------------------------------------------------------------------------
atr768:
        mvi     B, PALTBL shr 8
        xra     a
    @a768l:
        push    psw
        call    @u1To8
        call    @u1To8
        call    @u1To8
        pop psw
        dcr a
        jnz     @a768l
        ret
    @u1To8:
        mov     c,m
        ldax    b
        stax    d
        inx d
        stax    d
        inx d
        stax    d
        inx d
        stax    d
        inx d
        stax    d
        inx d
        stax    d
        inx d
        stax    d
        inx d
        stax    d
        inx d
        inx     h
        ret



;------------------------------------------------------------------------
; распаковка линейного мультиколора 8x1
;------------------------------------------------------------------------
atrLinear:
        mvi     a, 3
        mvi     B, PALTBL shr 8
  @alslp:
        push    psw
        push    h
        mvi     a, 8             ;в трети 8 строк
  @althird:
        push    psw
        push    h
        mvi     a, 32            ;32 символа в строке
  @allin:
        push    psw
        push    h
        call    @alunp
        call    @alunp
        call    @alunp
        call    @alunp
        call    @alunp
        call    @alunp
        call    @alunp
        call    @alunp
        pop     h
        inx     h
        pop     psw
        dcr     a
        jnz     @allin
        pop     h
        inr     h
        pop     psw
        dcr     a
        jnz     @althird
        pop     h
        push    d
        lxi     d, 2048
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @alslp
        ret
  @alunp:
        mov     c, m
        ldax    b
        stax    d
        inx     d
        push    d
        lxi     d, 32
        dad     d
        pop     d
        ret

;------------------------------------------------------------------------
; распаковка не линейного мультиколора 8x1
;------------------------------------------------------------------------
atrNLinear:
        mvi     a, 3
        mvi     B, PALTBL shr 8
  @anslp:
        push    psw
        push    h
        mvi     a, 8             ;в трети 8 строк
  @anthird:
        push    psw
        push    h
        mvi     a, 32            ;32 символа в строке
  @anlin:
        push    psw
        push    h
        UNLATR
        UNLATR
        UNLATR
        UNLATR
        UNLATR
        UNLATR
        UNLATR
        UNLATR
        pop     h
        inx     h
        pop     psw
        dcr     a
        jnz     @anlin
        pop     h
        push    d
        lxi     d, 32
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @anthird
        pop     h
        push    d
        lxi     d, 2048
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @anslp
        ret


;------------------------------------------------------------------------
; распаковка линейного мультиколора 8x2
;------------------------------------------------------------------------
atr3072:
        mvi     a, 3
        mvi     B, PALTBL shr 8
  @alpmn:
        push    psw
        push    h
        mvi     a, 8             ;в трети 8 строк
  @alpth:
        push    psw
        push    h
        mvi     a, 32            ;32 символа в строке
  @alpln:
        push    psw
        push    h
        call    @apunp
        call    @apunp
        call    @apunp
        call    @apunp
        pop     h
        inx     h
        pop     psw
        dcr     a
        jnz     @alpln
        pop     h
        push    d
        lxi     d, 32*4
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @alpth
        pop     h
        push    d
        lxi     d, 1024
        dad     d
        pop     d
        pop     psw
        dcr     a
        jnz     @alpmn
        ret
  @apunp:
        mov     c, m
        ldax    b
        stax    d
        push    d
        lxi     d, 32
        dad     d
        pop     d
        inx     d
        stax    d
        inx     d
        ret










MsxCol: db 0            ; 0 на msx
        db 1            ; 1 на msx
        db 2            ; 2 на msx
        db 3            ; 3 на msx
        db 4            ; 4 на msx
        db 5            ; 5 на msx
        db 8            ; 6 на msx бордовый - красный (8) на ПК8000
        db 7            ; 7 на msx
        db 8            ; 8 на msx
        db 9            ; 9 на msx
        db 10           ; 10 на msx
        db 11           ; 11 на msx
        db 2            ; 12 на msx - зеленый (2) на ПК8000
        db 13           ; 13 на msx
        db 14           ; 14 на msx
        db 15           ; 15 на msx

ZXCol:  db 0            ; 0 на zx - черный (0) на ПК8000
        db 4            ; 1 на zx - синий (4) на ПК8000
        db 8            ; 2 на zx - красный (8) на ПК8000
        db 4+8          ; 3
        db 2            ; 4 на zx - зеленый (2) на ПК8000
        db 4+2          ; 5
        db 8+2          ; 6
        db 4+8+2        ; 7




END
