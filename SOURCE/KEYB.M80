$title  ('Keyboard scanner')
        NAME    KEYB

        CSEG


        extrn SetVec
        extrn DelVec


        public KbHit
        public GetCh
        public KbInit
        public KbDone




  AUTOTIME      equ 50          ; задержка включения автоповтора
  AUTOSPD       equ 5           ; скорость автоповтора = (50 / AUTOSPD) нажатий в секунду


;------------------------------------------------------------------------
; Инициализация драйвера клавиатуры
;------------------------------------------------------------------------
KbInit:
        ; очищаем буфер клавиатуры
        lxi     H, inpBuf
        shld    ptBeg
        shld    ptEnd
        ; инициализируем данные драйвера
        xra     A
        sta     kFlags
        ; устанавливаем обработчик прерывания
        lxi     B, KEYISR
        lxi     D, OLDISR
        call    SetVec
        ret

;------------------------------------------------------------------------
; Удаление драйвера
;------------------------------------------------------------------------
KbDone:
        ; ждем отпускания всех клавиш
        lda     kFlags
        ora     A
        jnz     KbDone
        ; снимаем обработчик
        di
        lxi     B, OLDISR
        call    DelVec
        ; очищаем очередь клавиатуры
        lxi     H, 0FB85h
        shld    0FA2Ch
        shld    0FA2Ah
        ei
        ret



;------------------------------------------------------------------------
; проверяет наличие кода в буфере клавиатуры
;------------------------------------------------------------------------
; на выходе:
;    A  - 0: буфер пуст, 1: есть символ в буфере
KbHit:
        lhld    ptEnd
        xchg
        lhld    ptBeg
        call    CMPW
        mvi     A, 0
        rz
        inr     A
        ret



;------------------------------------------------------------------------
; чтение символа с клавиатуры с ожиданием
;------------------------------------------------------------------------
GetCh:
        call    KbHit
        ora     A
        jz      GetCh
        lhld    ptBeg
        mov     B, M
        inx     H
        lxi     D, endBuf
        call    CMPW
        jnz     @gtchd
        lxi     H, inpBuf
    @gtchd:
        shld    ptBeg
        mov     A, B
        ret







;------------------------------------------------------------------------
; добавляет символ в буфер клавиатуры
;------------------------------------------------------------------------
; на входе:
;     A - символ
kbPut:
        lhld    ptEnd
        lxi     D, endBuf
        mov     M, A
        inx     H
        call    CMPW
        jnz     @kbpd
        lxi     H, inpBuf
    @kbpd:
        shld    ptEnd
        ret

;------------------------------------------------------------------------
; сравнение двух 16-битных слов HL и DE
;------------------------------------------------------------------------
; на выходе: флаги как при сравнении 8-битных данных
CMPW:
        mov     A, H            ; сравниваем старшие байты
        sub     D
        rnz                     ; if (z==0) exit
        mov     A, L            ; сравниваем младшие байты
        sub     E
        ret


;------------------------------------------------------------------------
; чтение строки клавиатурной матрицы
;------------------------------------------------------------------------
; на входе:
;    A  - номер строки матрицы [0..9]
; на выходе:
;    A  - строка (единичные биты - нажатые клавиши)
;    C  - номер строки матрицы [0..9]
kbColumn:
        ani     0Fh
        mov     C, A
        in      82h
        ani     0F0h
        ora     C
        out     82h
        in      81h
        cma
        ret


;------------------------------------------------------------------------
; преобразование строки матрицы и бита в скан-код
;------------------------------------------------------------------------
; на входе:
;    kColumn
;    kMask
; на выходе:
;    A  - скан-код клавиши
kbCode:
        lda     kColumn
        ani     0Fh
        rlc
        rlc
        rlc
        mov     E, A            ; E = column * 8
        lda     kMask
        ana     A
        rz
        mvi     C, 0
    @cbclp:
        rrc
        jc      @cbcdn
        inr     C
        jmp     @cbclp
    @cbcdn:
        mov     A, C
        add     E
        ret



;------------------------------------------------------------------------
; Обработчик прерывания - сканер матрицы клавиатуры
;------------------------------------------------------------------------
KEYISR:
        lda     kFlags
        ora     A
        jnz     @kbRep
        ; нет нажатых клавиш, сканируем всю матрицу
  @kbDoSc:
        mvi     C, 0
  @kbScAll:
        mov     A, C
        cpi     10
        jnc     OLDISR          ; ничего не нашли, уходим
        call    kbColumn
        ora     A
        jnz     @kbPres
        inr     C
        jmp     @kbScAll
    @kbPres:    ; что-то нажали
        mov     B, A
        cma
        inr     A
        ana     B               ; выделяем крайний справа бит = x & (-x)
        sta     kMask
        mov     A, C
        sta     kColumn
        mvi     A, AUTOTIME
        sta     kTimer          ; выставляем время до следующего автонажатия
        mvi     A, 1
        sta     kFlags          ; устанавливаем флаг нажатия

  @kbIns: ; отправляет в буфер код нажатой клавиши
        call    kbCode
        call    kbPut
        jmp     OLDISR

  @kbRep:     ; уже имеется нажатая клавиша
        lda     kColumn
        call    kbColumn
        mov     B, A
        lda     kMask
        ana     B
        jz      @kbRel
        ; проверяем не пора ли включать автоповтор?
        lda     kTimer
        dcr     A
        sta     kTimer
        jnz     OLDISR
        ; время ожидания истекло, заносим скан-код в буфер клавиатуры
        mvi     A, AUTOSPD
        sta     kTimer
        jmp     @kbIns

  @kbRel: ; отжали клавишу
        xra     A
        sta     kFlags          ; снимаем флаг нажатия
OLDISR: ; jmp to old INT
        ret
        ret
        ret





DSEG

;------------------------------------------------------------------------
; Данные сканера
;------------------------------------------------------------------------

  kFlags:       ds 1            ; флаг наличия нажатой кнопки
  kMask:        ds 1            ; бит нажатой клавиши
  kColumn:      ds 1            ; строка нажатой клавиши
  kTimer:       ds 1            ; таймер для отслеживания зедержек автоповтора

  inpBuf:       ds 7            ; буфер приема скан-кодов
  endBuf:       ds 1

  ptBeg:        ds 2            ; указатель на начало буфера
  ptEnd:        ds 2            ; указатель на конец буфера





END
